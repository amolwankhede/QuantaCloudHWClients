#include <WiFi.h>
#include <HTTPClient.h>
#include <ArduinoJson.h>
#include <time.h>

// Wi-Fi credentials
const char *ssid = "Zapisco";
const char *password = "zzzzz@757!";

// API details
const char *serverUrl = "https://api.quantaflare.com/v1/Data/multi-stream/6c603fd2-97bc-4693-b235-686b51c2afa9?getresponse=true";
const char *apiKey = "xxx";

// NTP Server settings
const char *ntpServer = "pool.ntp.org";
const long gmtOffset_sec = 0;
const int daylightOffset_sec = 0;

void setup()
{
  Serial.begin(115200);

  // Connect to Wi-Fi
  WiFi.begin(ssid, password);
  while (WiFi.status() != WL_CONNECTED)
  {
    delay(1000);
    Serial.println("Connecting to WiFi...");
  }
  Serial.println("Connected to WiFi");

  // Initialize time from NTP
  configTime(gmtOffset_sec, daylightOffset_sec, ntpServer);
}

// Get current time in milliseconds
unsigned long long getTimeInMillis()
{
  struct timeval tv;
  gettimeofday(&tv, NULL);
  return (unsigned long long)(tv.tv_sec) * 1000LL + (tv.tv_usec / 1000LL);
}

// Generate a random Lateral Torque value between 0 and 100
float generateLateralTorque()
{
  return random(0, 10001) / 100.0; // Generates a value between 0.00 and 100.00
}

// Generate a random pitch value between 22.00 and 25.00
float generatePitch()
{
  return random(2200, 2501) / 100.0;
}

// Generate a random RPM value between 300 and 700
int generateRPM()
{
  return random(300, 701); // Generates a value between 300 and 700
}

// Generate a random Depth value between 2.00 and 4.00
float generateDepth()
{
  return random(200, 401) / 100.0; // Generates a value between 2.00 and 4.00
}

void loop()
{
  if (WiFi.status() == WL_CONNECTED)
  {
    HTTPClient http;

    http.begin(serverUrl);
    http.addHeader("Content-Type", "application/json");
    http.addHeader("x-api-key", apiKey);

    // Create JSON structure
    StaticJsonDocument<768> doc;  // Increased size for two streams
    JsonArray root = doc.to<JsonArray>();

    // Add timestamp (same for both streams)
    unsigned long long timestamp = getTimeInMillis();
    root.add(timestamp);
    
    // Create the nested array structure
    JsonArray dataArray = root.createNestedArray();
    
    // ===== Stream 21: Torque and Pitch =====
    JsonArray stream21Data = dataArray.createNestedArray();
    stream21Data.add(21); // Stream ID
    
    JsonArray stream21Keys = stream21Data.createNestedArray();
    stream21Keys.add(86);  // Torque stream key
    stream21Keys.add(87);  // Pitch stream key
    
    JsonArray stream21Values = stream21Data.createNestedArray();
    stream21Values.add(String(generateLateralTorque(), 2));
    stream21Values.add(String(generatePitch(), 2));
    
    // ===== Stream 22: RPM and Depth =====
    JsonArray stream22Data = dataArray.createNestedArray();
    stream22Data.add(22); // Stream ID
    
    JsonArray stream22Keys = stream22Data.createNestedArray();
    stream22Keys.add(88);  // RPM stream key
    stream22Keys.add(89);  // Depth stream key
    
    JsonArray stream22Values = stream22Data.createNestedArray();
    stream22Values.add(String(generateRPM()));
    stream22Values.add(String(generateDepth(), 2));
    
    String jsonString;
    serializeJson(doc, jsonString);

    Serial.println("\nSending data: " + jsonString);

    int httpResponseCode = http.POST(jsonString);

    if (httpResponseCode > 0)
    {
      String response = http.getString();
      Serial.println("HTTP Response code: " + String(httpResponseCode));
      Serial.println("Response body: " + response);
    }
    else
    {
      Serial.print("Error on sending POST: ");
      Serial.println(httpResponseCode);
      Serial.println("Error: " + http.errorToString(httpResponseCode));
    }

    http.end();
  }
  else
  {
    Serial.println("WiFi not connected");
  }

  delay(7000); // Wait for 7 seconds before sending the next payload
}
